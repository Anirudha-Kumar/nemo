#! /bin/csh -f
#
#  5-mar-2004     for nbody-school            PJT 
#
#  Example usage:
#  CHARA:: manybody_pkg/install_manybody cvs=0 pkg=`pwd`/manybody_pkg > & LOG1
#  

set pgplot=1
set gcc=0
set falcon=1
set cvs=1

#  things that have versions
set gsl=1.4
set fewbody=0.21
set glstarview=0.6-devel
set hnbody=1.0.3-linux-x86

set pkg=/home/manybody_pkg/

foreach _a ($*)
  set $_a
end

if (-d /astromake) then
  source /astromake/astromake_start
endif

set fltk=$pkg/fltk/fltk-1.1.4-source.tar.gz

# remove the old manybody
rm -rf manybody

# get all the new stuff via CVS and/or tarballs
if ($cvs) then
  echo CVS: manybody/starcluster
  cvs -Q co manybody/starcluster
  echo CVS: manybody/starlab
  cvs -Q co manybody/starlab
  cd manybody
  echo CVS: partiview
  cvs -Q co partiview
  echo CVS: nemo
  cvs -Q co nemo
  echo CVS: pgplot
  (cd nemo; mkdir local; cvs -Q co pgplot)
  echo TAR: $fltk
  tar -C partiview/src -zxf $fltk
else 
  echo TAR: 
  mkdir manybody
  cd manybody
  tar zxf $pkg/nemo/nemo.tar.gz
  tar zxf $pkg/starlab/starlab.tar.gz
  tar zxf $pkg/partiview/partiview.tar.gz
  (cd partiview/src ; tar zxf $fltk)
endif


#                                                   starlab
pushd starlab
  source AAA_SOURCE_ME
  echo STARLAB-COUNT: `ls $STARLAB/bin|wc -l`
popd

#                                                   fltk (internal build only)
pushd partiview/src/fltk-1.1.4
  ./configure >& install.log
  make >>& install.log
  if ($status) echo ERRORS compiling fltk, check install.log
popd

#                                                   partiview
pushd partiview/src
  ./configure --with-fltk=`pwd`/fltk-1.1.4 >& install.log
  make >>& install.log
  if ($status) echo ERRORS compiling partiview, check install.log
  cp partiview $STARLAB/bin
popd

#                                                   nemo
pushd nemo
  source AAA_SOURCE_ME
  echo NEMOBIN-COUNT: `ls $NEMOBIN|wc -l`
popd

# firstn
pushd $NEMO/usr/aarseth/firstn
  make firstn
  cp firstn $NEMOBIN
popd

# nbody6
mkdir $NEMO/usr/aarseth/nbody6
pushd $NEMO/usr/aarseth/nbody6
  tar zxf $pkg/aarseth/nbody6/nbody6.tar.Z
  (cd Ncode ; make nbody6 ; cp nbody6 $NEMOBIN)
popd

# GalactICS
pushd $NEMO/usr/kuijken/GalactICS-exp/src
   make all
   cp dbh genhalo genbulge gendisk diskdf tobinary getfreqs dbh vcirc plothalo plotforce mergerv $NEMOBIN
popd

#                                                   GSL
pushd $NEMO/local
  tar zxf $pkg/gsl/gsl-$gsl.tar.gz
  cd gsl-$gsl
  ./configure --prefix=$NEMO/host/$NEMOHOST  >& install.log
  if ($status) echo ERRORS 1 compiling partiview, check install.log
  make >>& install.log
  if ($status) echo ERRORS 2 compiling partiview, check install.log
  make install >>& install.log
  if ($status) echo ERRORS 3 compiling partiview, check install.log
popd

#                                                   fewbody, glstarview
pushd $NEMO/usr/fregeau
  tar zxf $pkg/fregeau/fewbody-$fewbody.tar.gz
  pushd fewbody-$fewbody
    make all  LIBFLAGS="-L$NEMOLIB -lgsl -lgslcblas -lm" >& install.log
    if ($status) echo ERRORS compiling fewbody, check install.log
    cp cluster triplebin binbin binsingle $NEMOBIN
  popd
  tar zxf $pkg/fregeau/glstarview-$glstarview.tar.gz
  pushd glstarview-$glstarview
    make -f Makefile.rh9 all
    if ($status) echo ERRORS compiling glstarview, check install.log
    cp glstarview $NEMOBIN
  popd
popd

#                                                   HNbody (binary only)
pushd $NEMO/usr
  tar zxf $pkg/hnbody/hnbody-$hnbody.tgz
  ln -s hnbody-$hnbody hnbody
  foreach bin (ems  hnarith  hnbody  hnconvert  hnpaste  hnstats  hntac  lunar)
    ln -s `pwd`/hnbody-$hnbody/bin/$bin $NEMOBIN
  end
popd

#                                                   ds9
pushd $NEMOBIN
  tar zxf $pkg/ds9/ds9.tar.gz
popd


# -- end of install --
