#! /bin/csh -f
#
#  5-mar-2004     for nbody-school                    PJT 
# 20-apr-2004     minor things for fam04              pjt
# 12-jul-2005     for 2nd nbody-school MODEST5c       PJT
#
#  Removed any local directory called manybody and installs a new manybody tree
#  The default should be executed from the home directory, such that
#  MANYBODY becomes $HOME/manybody
#
#  Example usage:
#  CHARA:: manybody_pkg/install_manybody cvs=0 pkg=`pwd`/manybody_pkg > & LOG1
#  

set pgplot=1
set gcc=0
set falcon=1
set cvs=0

#  things that have versions
set gsl=1.4
set fewbody=0.21
set glstarview=0.6-devel
set hnbody=1.0.3-linux-x86
set acs=lite-1.2.1
set ruby=1.8.2

set pkg=/home/manybody_pkg/

foreach _a ($*)
  set $_a
end

if (-d /astromake) then
  source /astromake/astromake_start
endif

set fltk=$pkg/fltk/fltk-1.1.4-source.tar.gz

# remove the old manybody
rm -rf manybody


# get all the new stuff via CVS and/or tarballs
if ($cvs) then
  echo CVS: manybody/starcluster
  cvs -Q co manybody/starcluster
  echo CVS: manybody/starlab
  cvs -Q co manybody/starlab
  cd manybody
  echo CVS: partiview
  cvs -Q co partiview
  echo CVS: nemo
  cvs -Q co nemo
  echo CVS: pgplot
  (cd nemo; mkdir local; cvs -Q co pgplot)
  echo TAR: $fltk
  tar -C partiview/src -zxf $fltk
else 
  echo TAR: 
  mkdir manybody
  cd manybody
  tar zxf $pkg/nemo/nemo_cvs.tar.gz
  tar zxf $pkg/starlab/starlab_cvs.tar.gz
  tar zxf $pkg/partiview/partiview_cvs.tar.gz
  (cd partiview_cvs/src ; tar zxf $fltk)
endif
tar zxf $pkg/acs/acs-$acs.tgz
tar zxf $pkg/ruby/ruby-$ruby.tar.gz

#                             root of $MANYBODY
set root=`pwd`

#PJT
#goto testing

#                                                   ruby
pushd ruby-1.8.2
  ./configure --prefix=$root/opt
  make
  make install
popd


#                                                   acs-lite
pushd acs
  setenv ACSROOT `pwd`
  source $ACSROOT/bin/acsstart.cshrc
  make acscode
  if (0) then
    # the long long path to make documentation
    # also assumes we have a local ruby
    cd $ACSROOT/src/rdoc*
    ruby install.rb
    cd $ACSROOT
    make
  endif
popd

#                                                   starlab
pushd starlab_cvs
  ./install-starlab
  echo STARLAB-COUNT: `ls $STARLAB/bin|wc -l`
popd


#                                                   fltk (internal build only)
pushd partiview_cvs/src/fltk-1.1.4
  ./configure >& install.log
  make >>& install.log
  if ($status) echo ERRORS compiling fltk, check install.log
popd

#                                                   partiview
pushd partiview_cvs/src
  ./configure --with-fltk=`pwd`/fltk-1.1.4 >& install.log
  make >>& install.log
  if ($status) echo ERRORS compiling partiview, check install.log
  # cp partiview $STARLAB/bin
popd

testing:
#                                                   nemo
pushd nemo_cvs
  source AAA_SOURCE_ME
  make bins
  echo NEMOBIN-COUNT: `ls $NEMOBIN|wc -l`
popd

# firstn
pushd $NEMO/usr/aarseth/firstn
  make firstn
  cp firstn $NEMOBIN
popd

# nbody6
mkdir $NEMO/usr/aarseth/nbody6
pushd $NEMO/usr/aarseth/nbody6
  tar zxf $pkg/aarseth/nbody6/nbody6.tar.Z
  (cd Ncode ; make nbody6 ; cp nbody6 $NEMOBIN)
popd

# GalactICS
pushd $NEMO/usr/kuijken/GalactICS-exp/src
   make -f Makefile.nemo all install
   mknemo mkkd95
popd

#                                                   GSL
pushd $NEMO/local
  tar zxf $pkg/gsl/gsl-$gsl.tar.gz
  cd gsl-$gsl
  ./configure --prefix=$NEMO/host/$NEMOHOST  >& install.log
  if ($status) echo ERRORS 1 compiling gsl, check install.log
  make >>& install.log
  if ($status) echo ERRORS 2 compiling gsl, check install.log
  make install >>& install.log
  if ($status) echo ERRORS 3 compiling gsl, check install.log
popd

#                                                   fewbody, glstarview
pushd $NEMO/usr/fregeau
  tar zxf $pkg/fregeau/fewbody-$fewbody.tar.gz
  pushd fewbody-$fewbody
    make all  \
	CFLAGS="-I$NEMO/host/include -Wall -O3" \
	LIBFLAGS="-L$NEMOLIB -lgsl -lgslcblas -lm" >& install.log
    if ($status) echo ERRORS compiling fewbody, check install.log
    cp cluster triplebin binbin binsingle $NEMOBIN
  popd
  tar zxf $pkg/fregeau/glstarview-$glstarview.tar.gz
  pushd glstarview-$glstarview
    make -f Makefile.rh9 all
    if ($status) echo ERRORS compiling glstarview, check install.log
    cp glstarview $NEMOBIN
  popd
popd

#                                                   HNbody (binary only)
pushd $NEMO/usr
  tar zxf $pkg/hnbody/hnbody-$hnbody.tgz
  ln -s hnbody-$hnbody hnbody
  foreach bin (ems  hnarith  hnbody  hnconvert  hnpaste  hnstats  hntac  lunar)
    mv hnbody/bin/$bin $NEMOBIN
  end
popd

#                                                   ds9
pushd $NEMOBIN
  tar zxf $pkg/ds9/ds9.tar.gz
popd

#  xyz stuff
$NEMO/src/scripts/nemo.vogl
mknemo xyzview snapxyz

# -- end of install --
