#! /bin/csh -f
#
#  5-mar-2004     for 1st nbody-school                             PJT 
# 20-apr-2004     minor things for fam04                           pjt
# 12-jul-2005     many changed for 2nd nbody-school MODEST5c       PJT
#
#  Removed any local directory called manybody and installs a new manybody tree
#  The default should be executed from the home directory, such that
#  MANYBODY becomes $HOME/manybody
#
#  Example usage:
#  manybody_pkg/install_manybody cvs=0 pkg=`pwd`/manybody_pkg > & LOG1
#  



#  things that are 0 or 1
set pgplot=1
set falcon=1
set cvs=0

#  things that have versions if non-zero, else won't be installed
set gcc=0

#  things that have versions and could in theory change
set gsl=1.4
set fewbody=0.21
set glstarview=0.6-devel
set hnbody=1.0.3-linux-x86
set acs=lite-1.2.1
set ruby=1.8.2
set fltk=1.1.4

#  root directory where all the tar ball packages live from which to install
set pkg=`pwd`/manybody_pkg
set sys=`uname -s | tr '[A-Z]' '[a-z]'`

#  poor mans command line parser
foreach _a ($*)
  set $_a
end

if (! -e $pkg/aarseth) then
  echo pkg=$pkg does not seem to be a manybody_pkg directory
  exit 1
else
  echo `date` Using pkg=$pkg 
endif

if ($?ASTROMAKE) then
  source $ASTROMAKE/astromake_start
endif


# remove the old manybody
rm -rf manybody


# get all the new stuff via CVS and/or tarballs
if ($cvs) then
  echo CVS: manybody/starcluster
  cvs -Q co manybody/starcluster
  echo CVS: manybody/starlab
  cvs -Q co manybody/starlab
  cd manybody
  echo CVS: partiview
  cvs -Q co partiview
  echo CVS: nemo
  cvs -Q co nemo
  echo CVS: pgplot
  (cd nemo; mkdir local; cvs -Q co pgplot)
else 
  mkdir manybody
  cd manybody
  tar zxf $pkg/nemo/nemo_cvs.tar.gz
  tar zxf $pkg/starlab/starlab_cvs.tar.gz
  tar zxf $pkg/starcluster/starcluster_cvs.tar.gz
  tar zxf $pkg/partiview/partiview_cvs.tar.gz
endif
tar zxf $pkg/acs/acs-$acs.tgz
tar zxf $pkg/ruby/ruby-$ruby.tar.gz

#                             root of $MANYBODY
set root=`pwd`
mkdir $root/tmp
mkdir $root/opt

set path=(. $root/bin $root/opt/bin $path); rehash

#PJT
goto testing
testing:

#                                                   gcc
if ($gcc != 0) then
  echo `date` Installing gcc $  set log=$root/tmp/gcc.log
  tar jxf $pkg/gcc/gcc-$gcc.tar.bz2
  pushd gcc-$gcc  >& /dev/null
    # a simple gcc. note f77 is replaced by g95 starting gcc4
    set copts=(--prefix=$root/opt  --enable-threads --enable-languages=c,c++,f77)
    (mkdir _build; cd _build; ../configure $copts; make bootstrap; make; make install) >& $log
  popd >& /dev/null
  rehash
endif

#                                                   cvsutils

echo  `date` Installing cvsutils
tar zxf $pkg/tools/cvsutils-0.2.3.tar.gz
pushd cvsutils-0.2.3 >& /dev/null
  set log=$root/tmp/cvsutils.log
  ./configure --prefix=$root/opt   >& $log
  make                            >>& $log
  make install                    >>& $log
popd >& /dev/null
rehash

#                                                   ruby
echo  `date` Installing ruby
pushd ruby-1.8.2 >& /dev/null
  set log=$root/tmp/ruby.log
  ./configure --prefix=$root/opt   >& $log
  make                            >>& $log
  make install                    >>& $log
popd >& /dev/null
rehash

#                                                   acs-lite
echo  `date` Installing acs
pushd acs >& /dev/null
  set log=$root/tmp/acs.log
  setenv ACSROOT `pwd`
  source $ACSROOT/bin/acsstart.cshrc
  make acscode >& $log
  if (0) then
    # the long long path to make documentation
    # also assumes we have a local ruby
    cd $ACSROOT/src/rdoc*
    ruby install.rb
    cd $ACSROOT
    make
  endif
popd >& /dev/null
rehash

#                                                   starlab
echo  `date` Installing starlab
pushd starlab_cvs >& /dev/null
  set log=$root/tmp/starlab.log
  ./install-starlab >& $log
  source starlab_start.csh
  echo STARLAB-COUNT: `ls $STARLAB_INSTALL_PATH/bin|wc -l`
popd >& /dev/null
rehash

#                                                   starcluster
echo  `date` Installing starcluster
pushd starcluster_cvs >& /dev/null
  set log=$root/tmp/starcluster.log
  make >& $log
  if ($status) echo ERRORS compiling starcluster, check $log
  make install prefix=$STARLAB_INSTALL_PATH >>& $log
  if ($status) echo ERRORS installing starcluster, check $log
  echo STARLAB+CLUSTER-COUNT: `ls $STARLAB_INSTALL_PATH/bin|wc -l`
popd >& /dev/null
rehash

#                                                   fltk (internal build only)
echo  `date` Installing fltk 
tar -C partiview_cvs/src -zxf $pkg/fltk/fltk-1.1.4-source.tar.gz
pushd partiview_cvs/src/fltk-1.1.4 >& /dev/null
  set log=$root/tmp/fltk.log
  ./configure >& $log
  make >>& $log
  if ($status) echo ERRORS compiling fltk, check $log
popd >& /dev/null
rehash

#                                                   partiview
echo  `date` Installing partiview
pushd partiview_cvs/src >& /dev/null
  set log=$root/tmp/partiview.log 
  ./configure --with-fltk=`pwd`/fltk-1.1.4 >& $log
  make >>& $log
  if ($status) echo ERRORS compiling partiview, check $log
  # cp partiview $STARLAB/bin
popd >& /dev/null

#                                                   nemo
echo  `date` Installing NEMO
pushd nemo_cvs >& /dev/null
  set log=$root/tmp/nemo.log 
  source AAA_SOURCE_ME  >& $log
  make bins >>& $log
  echo NEMO-COUNT: `ls $NEMOBIN|wc -l`
popd >& /dev/null
rehash

# firstn
echo  `date` Installing firstn
pushd $NEMO/usr/aarseth/firstn >& /dev/null
  set log=$root/tmp/firstn.log 
  make firstn >& $log
  if ($status) echo ERRORS compiling firstn, check $log  
  cp firstn $NEMOBIN
popd >& /dev/null
rehash

# nbody6
echo  `date` Installing nbody6
mkdir $NEMO/usr/aarseth/nbody6
pushd $NEMO/usr/aarseth/nbody6 >& /dev/null
  set log=$root/tmp/nbody6.log 
  tar zxf $pkg/aarseth/nbody6/nbody6.tar.Z
  (cd Ncode ; make nbody6 ; cp nbody6 $NEMOBIN) >& $log
  if ($status) echo ERRORS compiling nbody6, check $log  
popd >& /dev/null
rehash

# GalactICS
echo  `date` Installing galactICS
pushd $NEMO/usr/kuijken/GalactICS-exp/src >& /dev/null
   set log=$root/tmp/gal.log
   make -f Makefile.nemo all install >& $log
   mknemo mkkd95 >& $log
popd >& /dev/null
rehash

#                                                   GSL
echo  `date` Installing gsl
pushd $NEMO/local >& /dev/null
  set log=$root/tmp/gsl.log 
  tar zxf $pkg/gsl/gsl-$gsl.tar.gz
  cd gsl-$gsl
  ./configure --prefix=$NEMO/opt  >& $log
  if ($status) echo ERRORS 1 compiling gsl, check $log
  make >>& $log
  if ($status) echo ERRORS 2 compiling gsl, check $log
  make install >>& $log
  if ($status) echo ERRORS 3 compiling gsl, check $log
popd >& /dev/null
rehash

#                                                   fewbody, glstarview
echo  `date` Installing fewbody,glstarview
pushd $NEMO/usr/fregeau >& /dev/null
  set log=$root/tmp/fewbody.log 
  tar zxf $pkg/fregeau/fewbody-$fewbody.tar.gz
  pushd fewbody-$fewbody >& /dev/null
    make all  \
	CFLAGS="-I$NEMO/opt/include -Wall -O3" \
	LIBFLAGS="-L$NEMO/opt/lib -lgsl -lgslcblas -lm" >& $log
    if ($status) echo ERRORS compiling fewbody, check $log
    cp cluster triplebin binbin binsingle $NEMOBIN
  popd >& /dev/null
  tar zxf $pkg/fregeau/glstarview-$glstarview.tar.gz
  pushd glstarview-$glstarview
    make -f Makefile.rh9 all >>& $log
    if ($status) echo ERRORS compiling glstarview, check $log
    cp glstarview $NEMOBIN
  popd >& /dev/null
popd >& /dev/null
rehash
#                                                   HNbody (binary only)
if ($sys == linux) then
echo  `date` Installing hnbody- for linux only
pushd $NEMO/usr >& /dev/null
  tar zxf $pkg/hnbody/hnbody-$hnbody.tgz
  ln -s hnbody-$hnbody hnbody
  foreach bin (ems  hnarith  hnbody  hnconvert  hnpaste  hnstats  hntac  lunar)
    mv hnbody/bin/$bin $NEMOBIN
  end
popd >& /dev/null
rehash
endif

#                                                   ds9
if ($sys == linux) then
echo  `date` "Installing ds9 - for linux only"
pushd $NEMOBIN >& /dev/null
  tar zxf $pkg/ds9/ds9.tar.gz
popd >& /dev/null
rehash
endif

#  xyz stuff
echo  `date` Installing xyz from NEMO
  set log=$root/tmp/xyz.log 
  $NEMO/src/scripts/nemo.vogl  >& $log
  if ($status) echo ERRORS compiling vogl, check $log
  mknemo xyzview snapxyz      >>& $log
  if ($status) echo ERRORS compiling xyz, check $log

# EZ
echo  `date` Installing EZ
  set log=$root/tmp/ez.log 
  tar zxf $pkg/EZ/EZ_3_0_3.tar.gz
  pushd EZ_3_0_3 >& /dev/null
    if (0) then
       ./QUICK_INSTALL >& $log
    else
       # use: intel: ifort -w -132
       # or:  gnu:   g95/f95 -ffixed-line-length-132
       (cd make; make EZ COMP="ifort -w -132") >& $log
       if ($status) echo ERRORS compiling EZ, check $log
    endif
  popd >& /dev/null

# Tioga
echo  `date` Installing Tioga
  set log=$root/tmp/tioga.log
  tar zxf $pkg/Tioga/Tioga-1.0.A-linux.tar.gz
  pushd Tioga-1.0.A >& /dev/null
    (cd make; ruby extconf.rb ; make; make install) >& $log
    (cd tests; ruby ts_Tioga.rb) >>& $log
  popd >& /dev/null
rehash

# MMAS
echo  `date` Installing MMAS
  set log=$root/tmp/mmas.log
  tar zxf $pkg/mmas/mmas1.6.tar.gz 
  pushd mmas1.6 >& /dev/null
    make >& $log
    if ($status) echo ERRORS compiling mmas, check $log
  popd >& /dev/null
rehash

# FFTW v2, needed for starcrash
echo  `date` Installing FFTW
  set log=$root/tmp/fftw.log
  tar zxf $pkg/fftw/fftw-2.1.5.tar.gz
  pushd fftw-2.1.5 >& /dev/null
    ./configure --prefix=$root/opt   >& $log
    make >>& $log
    if ($status) echo ERRORS compiling fftw, check $log
    make install >>& $log
    if ($status) echo ERRORS installing fftw, check $log
  popd >& /dev/null
rehash

# StarCrash
echo  `date` Instaling StarCrash
  set log=$root/tmp/starcrash.log
  tar zxf $pkg/StarCrash/starcrash_serial.tar.gz 
  pushd serial >& /dev/null
    make testinput b2pos b2pos2 sphagr \
	FFTDIR=$root/fftw-2.1.5  FC=g77 CC=gcc  >& $log
    if ($status) echo ERRORS compiling starcrash, check $log
  popd >& /dev/null
rehash

# -- end of install --
echo  `date` Done.
