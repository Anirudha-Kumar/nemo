#! /bin/csh -f
#
#  P600/     116.510u 17.390s 2:41.66 82.8%     0+0k 0+0io 778435pf+0w
# P1600/

set pgplot=1
set intel=0
set gcc=0
set falcon=1
set anonymous=1
set cvsroot=0
set vogl=1

foreach _a ($*)
  set $_a
end
  
if ($?NEMO) then
  echo Cannot test if NEMO=$NEMO is present already
  exit 1
endif

if ($anonymous) then
 setenv CVSROOT :pserver:anonymous@cvs.astro.umd.edu:/home/cvsroot
endif

#if ($?CVSROOT == 0) then
#
#endif

#		pick another GNU compiler 
if ($gcc) then
   source /astromake/astromake_start
   astroload -v $gcc gcc
endif

#		pick intel compiler (ifort means >= 8.0)
if ($intel) then
  source /astromake/astromake_start
  astroload intel
  setenv CC icc
  setenv CXX icc
  setenv F77 ifort
endif

#		pick a certain CVS release
if (0) then
  set cvs_rev=(-r nemo_3_0_16)
else
  set cvs_rev=()
endif

#		get  the source
time cvs -Q checkout $cvs_rev nemo
if ($pgplot) then
   (mkdir nemo/local; cd nemo/local; cvs -Q checkout pgplot)
endif

# --disable-gsl
# --enable-single

cd nemo
if ($pgplot) then
  # first do a dummy to get the architecture libraries done.... a kludge
  ./configure 
  source nemo_start
  make dirs config_clean
  rm NEMORC.local
  # dir= not used ??
  src/scripts/pgplot.install dir=$NEMO/tmp

  #./configure --with-pgplot-prefix=/astromake/opt/pgplot --with-yapp=pgplot 
  #./configure --with-pgplot-prefix=/astromake/opt/pgplot
  ./configure --with-yapp=pgplot --with-pgplot-prefix=$NEMOLIB
else
 ./configure --with-yapp=ps 
endif
source nemo_start
make dirs config_extra scripts
source NEMORC.local
rehash
make libs
if ($vogl) $NEMO/src/scripts/nemo.vogl

src/scripts/testsuite -b

if ($falcon) then
if (-d usr/dehnen/falcON) then
  # falcON
  set fbins=(TestGrav gyrfalcON)
  set arch=i386_linux
  echo Compiling $fbins for $arch
  ( cd usr/dehnen/falcON; make ; make $fbins; cd $arch; cp $fbins $NEMOBIN)  >& install_falcON.log
else
  # YancNemo
  echo Compiling YancNemo
  ( cd usr/dehnen/yanc; mkdir -p lib; make ; make YancNemo mkking; cp YancNemo mkking $NEMOBIN )  >& install_yanc.log
endif
endif

# src/scripts/nemo.vogl

echo All done.
